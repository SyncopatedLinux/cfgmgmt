# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ambix
pkgname=ambix-o1
pkgver=0.2.10
pkgrel=1
pkgdesc="Ambisonic VST, LV2 plug-ins with variable order for use in Digital Audio Workstations"
arch=('x86_64' 'x86_64_v3')
url="https://github.com/kronihias/ambix"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libsoxr' 'libx11' 'libxext')
makedepends=('alsa-lib' 'cmake' 'eigen' 'fftw' 'freetype2' 'freeglut' 'gendesk'
'glu' 'libxcursor' 'libxinerama' 'jack' 'liblo' 'zita-convolver')
checkdepends=('lv2lint')
optdepends=('jack: for standalone applications'
            'lv2-host: for LV2 plugins'
            'vst-host: for VST plugins')
source=("$_name-$pkgver.tar.gz::https://github.com/kronihias/${_name}/archive/v${pkgver}.tar.gz"
        "${_name}-0.2.10-lv2_plugin_uri.patch::https://github.com/kronihias/ambix/pull/28/commits/fa5849377b0eeddbedd1d99768cfc9ad9933d751.patch"
        "${_name}-0.2.10-gcc9.1.0.patch::https://github.com/kronihias/ambix/pull/32/commits/30d9e8df4501d2dd5c35a09a14dd705413de44e7.patch")
sha256sums=('45aef208ba1953021bda7851f3cf458add8f3578521800fc690384a629447c2b'
            '898f353b916a56224cfdebbeffe8832541ab92d9dc73dc63ed8da72c4a8a1c7f'
            'c5ecb77a07a38953a514eebe5affc7b42334ffd5d07e0a35742234e340e90a65')

_plugin_names=('ambix_binaural' 'ambix_converter' 'ambix_decoder'
'ambix_directional_loudness' 'ambix_encoder_i2' 'ambix_encoder_i4'
'ambix_encoder_i6' 'ambix_encoder_i8' 'ambix_encoder' 'ambix_maxre'
'ambix_mirror' 'ambix_rotator' 'ambix_rotator_z' 'ambix_vmic' 'ambix_warp'
'ambix_widening')

_standalone_names=('ambix_binaural' 'ambix_converter' 'ambix_decoder'
'ambix_directional_loudness' 'ambix_encoder' 'ambix_mirror' 'ambix_rotator'
'ambix_vmic')

prepare() {
  cd "${_name}-$pkgver"
  # setting proper LV2 URIs:
  # https://github.com/kronihias/ambix/issues/27
  patch -Np1 -i "../${_name}-0.2.10-lv2_plugin_uri.patch"
  # fix for gcc >= 9.1.0
  # https://github.com/kronihias/ambix/issues/26
  patch -Np1 -i "../${_name}-0.2.10-gcc9.1.0.patch"

  declare -A _descriptions=(
    ['ambix_binaural']="Listen to Ambisonics with Headphones"
    ['ambix_converter']="Convert between Ambisonics Formats"
    ['ambix_decoder']="Playback Ambisonics with Loudspeakers"
    ['ambix_directional_loudness']="Amplify, Attenuate or Filter Out Certain Parts of the Spherical Soundfield"
    ['ambix_encoder']="Encode Audio for Ambisonics"
    ['ambix_mirror']="Adjust Symmetric Components in Ambisonics Streams"
    ['ambix_rotator']="Rotate Sounds for Ambisonics"
    ['ambix_vmic']="Virtual Microphone: Select part of the Soundfield"
  )
  declare -A _generic=(
    ['ambix_binaural']="Binaural Decoder"
    ['ambix_converter']="Ambisonics Formats Converter"
    ['ambix_decoder']="Single-band Decoded Ambisonics"
    ['ambix_directional_loudness']="Spherical Soundfield Amplifier"
    ['ambix_encoder']="Ambisonics Panning"
    ['ambix_mirror']="Soundfield Mirroring"
    ['ambix_rotator']="Ambisonics Sounds Rotator"
    ['ambix_vmic']="Ambisonics Virtual Microphone"
  )
  #TODO: fix desktop file names
  for _standalone in ${_standalone_names[@]}; do
    gendesk -f -n \
            --pkgname "${_standalone}" \
            --name "${_standalone}_o1" \
            --pkgdesc "${_description[${_standalone}]} o1" \
            --genericname "${_generic[${_standalone}]}" \
            --icon "ambix" \
            --categories "AudioVideo;Audio,Ambisonics"
  done
}

build() {
  cd "${_name}-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBUILD_LV2=ON \
        -DAMBI_ORDER=1 \
        -DWITH_ZITA_CONVOLVER=ON \
        -DOpenGL_GL_PREFERENCE=GLVND \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
  (
    cd lv2-ttl-generator
    make
  )
  (
    cd build/_bin
    ../../lv2-ttl-generator/generate-ttl.sh
  )
}

check() {
  cd "${_name}-$pkgver"
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -I "build/_bin/lv2/${_plugin}_o1.lv2/" \
      "https://github.com/kronihias/ambix/${_plugin}" \
      || echo "Known to fail: https://github.com/kronihias/ambix/issues/29"
  done
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libfftw3f_threads.so'
  'libfreetype.so' 'libzita-convolver.so')
  cd "${_name}-$pkgver"
  # install standalone applications
  for _standalone in ${_standalone_names[@]}; do
    install -vDm 755 "build/_bin/standalone/${_standalone}_standalone_o1" \
      "${pkgdir}/usr/bin/${_standalone}_o1"
  done
  # install vst2 plugins
  for _plugin in ${_plugin_names[@]}; do
    install -vDm 755 "build/_bin/vst/${_plugin}_o1.so" \
      "${pkgdir}/usr/lib/vst/${_plugin}_o1.so"
  done
  # install lv2 plugins
  for _plugin in ${_plugin_names[@]}; do
    install -vDm 755 "build/_bin/lv2/${_plugin}_o1.lv2/"*.so \
      -t "${pkgdir}/usr/lib/lv2/${_plugin}_o1.lv2/"
    install -vDm 644 "build/_bin/lv2/${_plugin}_o1.lv2/"*.ttl \
      -t "${pkgdir}/usr/lib/lv2/${_plugin}_o1.lv2/"
  done
  # XDG desktop integration
  install -vDm 644 *".desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 {AUTHORS,README.md} -t "${pkgdir}/usr/share/doc/${_name}"
}
